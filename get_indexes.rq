#+ summary: Get all nanopublication indexes (excluding incomplete ones)
#+ method: GET
#+ pagination: 1000

prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix dce: <http://purl.org/dc/elements/1.1/>
prefix dct: <http://purl.org/dc/terms/>

select ?np ?title ?date ?appends (count(distinct ?sub) as ?subindex_count) where {
  graph ?h {
    ?np a np:Nanopublication .
    ?np np:hasAssertion ?a .
    ?np np:hasPublicationInfo ?i .
  }
  graph ?a {
    optional { ?np npx:appendsIndex ?appends . }
    optional { ?np npx:includesSubindex ?sub . }
  }
  graph ?i {
    ?np a npx:NanopubIndex .
    filter not exists { ?np a npx:IncompleteIndex . }
    optional { ?np dce:title ?title . }
    optional { ?np dct:created ?date . }
  }
}
group by ?np ?title ?date ?appends
order by desc(?date)
