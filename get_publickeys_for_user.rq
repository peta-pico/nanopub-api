#+ summary: Get all public keys used for signatures linked to the given user
#+ method: GET

prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix dct: <http://purl.org/dc/terms/>
prefix dce: <http://purl.org/dc/elements/1.1/>
prefix pav: <http://purl.org/pav/>
#prefix pav1: <http://swan.mindinformatics.org/ontologies/1.2/pav/>
#prefix pav2: <http://purl.org/pav/2.0/>

select distinct ?pubkey where {
  graph npa:graph {
    ?np npa:hasHeadGraph ?h .
    ?np npa:hasValidSignatureForPublicKey ?pubkey .
  }
  graph ?h {
    ?np np:hasPublicationInfo ?i .
  }
  graph ?i {
    ?s npx:hasPublicKey ?pubkey .
    {
      # This is much faster under Virtuoso than '... (dct:creator|dce:creator|pav:createdBy) ...':
      { ?np dct:creator ?_user_iri } union 
      { ?np dce:creator ?_user_iri } union 
      { ?np pav:createdBy ?_user_iri }
      # pav1:createdBy and pav2:createdBy are currently not covered (also slows down Virtuoso)
    }
  }
}

