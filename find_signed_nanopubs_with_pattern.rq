#+ summary: Find all nanopublications for a given triple pattern signed with a given public key, in undefined order
#+ method: GET
#+ pagination: 1000
#+ endpoint_in_url: False

prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>

select ?np ?date ?retraction where {
  graph npa:graph {
    ?np npa:hasHeadGraph ?h .
    ?np npa:creationDay ?__day_iri .
    ?np npa:creationMonth ?__month_iri .
    ?np npa:creationYear ?__year_iri .
    ?np dct:created ?date .
    ?np npa:hasValidSignatureForPublicKey ?_pubkey_xsd_string .
  }
  graph ?h {
    ?np ?__graphpred_iri ?g .
  }
  graph ?g {
    ?__subj_iri ?__pred_iri ?__obj_iri .
    ?__subj_iri ?__pred_iri ?__objliteral .
  }
  optional {
    graph npa:graph {
      ?retraction npa:hasHeadGraph ?rh .
      ?retraction npa:hasValidSignatureForPublicKey ?_pubkey_xsd_string .
    }
    graph ?rh {
      ?retraction np:hasAssertion ?ra .
    }
    graph ?ra {
      ?somebody npx:retracts ?np .
    }
  }
}
