#+ summary: Find all nanopublications that contain the given text and are signed with the given public key, in undefined order
#+ method: GET
#+ pagination: 1000
#+ endpoint_in_url: False

prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix luc: <http://www.ontotext.com/owlim/lucene#>   # for GraphDB
#prefix bif: <http://www.openlinksw.com/schemas/bif#>  # for Virtuoso

select ?np ?v ?retraction where {
  graph npa:graph {
    ?np npa:hasHeadGraph ?h .
    ?np npa:creationDay ?__day_iri .
    ?np npa:creationMonth ?__month_iri .
    ?np npa:creationYear ?__year_iri .
    ?np npa:hasValidSignatureForPublicKey ?_pubkey_xsd_string .
  }
  graph ?h {
    ?np ?__graphpred_iri ?g .
  }
  graph ?g {
    { ?s ?p ?v . ?v luc:npIdx ?_text . }
    union
    { ?s ?p ?v .  ?v <bif:contains> ?_text . }
  }
  optional {
    graph npa:graph {
      ?retraction npa:hasHeadGraph ?rh .
      ?retraction npa:hasValidSignatureForPublicKey ?_pubkey_xsd_string .
    }
    graph ?rh {
      ?retraction np:hasAssertion ?ra .
    }
    graph ?ra {
      ?somebody npx:retracts ?np .
    }
  }
}
