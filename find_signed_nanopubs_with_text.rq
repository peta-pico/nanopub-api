#+ summary: Find signed nanopublications that contain the given text
#+ method: GET
#+ pagination: 1000
#+ endpoint_in_url: False

prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix luc: <http://www.ontotext.com/owlim/lucene#>   # for GraphDB
#prefix bif: <http://www.openlinksw.com/schemas/bif#>  # for Virtuoso

select ?np ?v ?date ?pubkey ?superseded ?retracted where {
  graph npa:graph {
    ?np npa:hasHeadGraph ?h .
    ?np npa:creationDay ?__day_iri .
    ?np npa:creationMonth ?__month_iri .
    ?np npa:creationYear ?__year_iri .
    ?np dct:created ?date .
    ?np npa:hasValidSignatureForPublicKey ?__pubkey_xsd_string .
    bind(?__pubkey_xsd_string as ?pubkey)
  }
  graph ?h {
    ?np ?__graphpred_iri ?g .
  }
  graph ?g {
    { ?s ?p ?v . ?v luc:npIdx ?_text . }
    union
    { ?s ?p ?v .  ?v <bif:contains> ?_text . }
  }
  bind(exists {
    graph npa:graph {
      ?newversion npa:hasHeadGraph ?nh .
      ?newversion npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?nh {
      ?newversion np:hasPublicationInfo ?ni .
    }
    graph ?ni {
      ?newversion npx:supersedes ?np .
    }
  } as ?superseded )
  bind(exists {
    graph npa:graph {
      ?retraction npa:hasHeadGraph ?rh .
      ?retraction npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?rh {
      ?retraction np:hasAssertion ?ra .
    }
    graph ?ra {
      ?somebody npx:retracts ?np .
    }
  } as ?retracted )
}
