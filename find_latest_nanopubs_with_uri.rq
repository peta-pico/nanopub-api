#+ summary: Find all nanopublications that contain the given URI with a signature of the given public key (not excluding invalid ones), sorted by recency
#+ method: GET
#+ pagination: 1000

prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix dct: <http://purl.org/dc/terms/>
prefix dce: <http://purl.org/dc/elements/1.1/>
prefix pav: <http://purl.org/pav/>
prefix prov: <http://www.w3.org/ns/prov#>

select distinct ?np ?date where {
  graph ?h {
    ?np a np:Nanopublication .
    ?np np:hasPublicationInfo ?pubinfo .
    { ?np np:hasAssertion ?g } union
    { ?np np:hasProvenance ?g } union
    { ?np np:hasPublicationInfo ?g }
  }
  graph ?g {
    { ?_ref_iri ?p ?o } union
    { ?s ?_ref_iri ?o } union
    { ?s ?p ?_ref_iri }
  }
  graph ?pubinfo {
    ?sig npx:hasPublicKey ?_publickey .
    ?np (dct:creator|dce:creator|pav:createdBy) ?__creator_iri .
    ?np (dct:created|pav:createdOn|prov:generatedAtTime) ?date .
  }
}
order by desc(?date)
