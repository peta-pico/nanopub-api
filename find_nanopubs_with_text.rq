#+ summary: Find all nanopublications that contain the given text
#+ method: GET
#+ pagination: 1000
#+ endpoint_in_url: False

prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix luc: <http://www.ontotext.com/owlim/lucene#>   # for GraphDB
#prefix bif: <http://www.openlinksw.com/schemas/bif#>  # for Virtuoso

select ?np ?v where {
  graph npa:graph {
    ?np npa:hasHeadGraph ?h .
    ?np npa:creationDay ?__day_xsd_date .
    ?np npa:creationMonth ?__month_xsd_gYearMonth .
    ?np npa:creationYear ?__year_xsd_gYear .
  }
  graph ?h {
    { ?np np:hasAssertion ?g } union
    { ?np np:hasProvenance ?g } union
    { ?np np:hasPublicationInfo ?g }
  }
  graph ?g {
    { ?s ?p ?v . ?v luc:npIdx ?_text . }
    union
    { ?s ?p ?v .  ?v <bif:contains> ?_text . }
  }
}
