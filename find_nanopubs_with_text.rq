#+ summary: Find nanopublications that contain the given text
#+ method: GET
#+ pagination: 1000
#+ endpoint_in_url: False

prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix dct: <http://purl.org/dc/terms/>
prefix luc: <http://www.ontotext.com/owlim/lucene#>   # for GraphDB
#prefix bif: <http://www.openlinksw.com/schemas/bif#>  # for Virtuoso

select ?np ?graphpred ?subj ?pred ?v ?date ?pubkey where {
  graph npa:graph {
    ?np npa:hasHeadGraph ?h .
    ?np npa:creationDay ?__day_iri .
    ?np npa:creationMonth ?__month_iri .
    ?np npa:creationYear ?__year_iri .
    ?np dct:created ?date .
  }
  graph ?h {
    ?np ?__graphpred_iri ?g .
    bind(?__graphpred_iri as ?graphpred)
  }
  graph ?g {
    { ?subj ?pred ?v . ?v luc:npIdx ?_text . }
    union
    { ?subj ?pred ?v .  ?v <bif:contains> ?_text . }
  }
  optional {
    graph npa:graph {
      ?np npa:hasValidSignatureForPublicKey ?pubkey .
    }
    # This duplication is a workaround for a Virtuoso bug (otherwise <bif:contains> above overgenerates):
    graph npa:graph {
      ?np npa:hasValidSignatureForPublicKey ?pubkey .
    }
  }
}
