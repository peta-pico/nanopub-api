#+ summary: Get all users, i.e. nanopublication creators that are linked to a valid signature
#+ method: GET
#+ endpoint_in_url: False

prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix dct: <http://purl.org/dc/terms/>
prefix foaf: <http://xmlns.com/foaf/0.1/>

select ?user ?name ?intronp ?date ?pubkey where {
  graph npa:graph {
    ?intronp npa:hasHeadGraph ?h .
    ?intronp npa:hasValidSignatureForPublicKey ?pubkey .
    ?intronp dct:created ?date .
  }
  graph ?h {
    ?intronp np:hasAssertion ?a .
  }
  graph ?a {
    ?keydeclaration npx:declaredBy ?user .
    optional {
      ?user foaf:name ?name .
    }
  }
  filter not exists {
    graph npa:graph {
      ?retraction npa:hasHeadGraph ?rh .
      ?retraction npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?rh {
      ?retraction np:hasAssertion ?ra .
    }
    graph ?ra {
      ?somebody npx:retracts ?intronp .
    }
  }
  filter not exists {
    graph npa:graph {
      ?superseding npa:hasHeadGraph ?sh .
      ?superseding npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?sh {
      ?superseding np:hasPublicationInfo ?si .
    }
    graph ?si {
      ?superseding npx:supersedes ?intronp .
    }
  }
}
order by ?user desc(?date)
