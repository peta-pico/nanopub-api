#+ summary: Find nanopublications that contain the given URI
#+ method: GET
#+ pagination: 1000
#+ endpoint_in_url: False

prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix dct: <http://purl.org/dc/terms/>

select ?np ?graphpred ?subj ?pred ?obj ?date ?pubkey where {
  graph npa:graph {
    ?np npa:hasHeadGraph ?h .
    ?np npa:creationDay ?__day_iri .
    ?np npa:creationMonth ?__month_iri .
    ?np npa:creationYear ?__year_iri .
    ?np dct:created ?date .
  }
  graph ?h {
    ?np ?__graphpred_iri ?g .
    bind(?__graphpred_iri as ?graphpred)
  }
  graph ?g {
    { ?_ref_iri ?p ?o . bind(?_ref_iri as ?subj) bind(?p as ?pred) bind(?o as ?obj) } union
    { ?s ?_ref_iri ?o . bind(?s as ?subj) bind(?_ref_iri as ?pred) bind(?o as ?obj) } union
    { ?s ?p ?_ref_iri . bind(?s as ?subj) bind(?p as ?pred) bind(?_ref_iri as ?obj) }
  }
  optional {
    graph npa:graph {
      ?np npa:hasValidSignatureForPublicKey ?pubkey .
    }
  }
}
