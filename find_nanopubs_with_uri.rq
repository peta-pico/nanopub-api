#+ summary: Find all nanopublications that contain the given URI, in undefined order
#+ method: GET
#+ pagination: 1000

prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

select ?np where {
  graph npa:graph {
    ?np npa:hasHeadGraph ?h .
    ?np npa:creationDay ?__day_xsd_date .
    ?np npa:creationMonth ?__month_xsd_gYearMonth .
    ?np npa:creationYear ?__year_xsd_gYear .
  }
  graph ?h {
    { ?np np:hasAssertion ?g } union
    { ?np np:hasProvenance ?g } union
    { ?np np:hasPublicationInfo ?g }
  }
  graph ?g {
    { ?_ref_iri ?p ?o } union
    { ?s ?_ref_iri ?o } union
    { ?s ?p ?_ref_iri }
  }
}
