#+ summary: Get all nanopublication indexes (excluding incomplete ones)
#+ method: GET
#+ pagination: 1000
#+ endpoint_in_url: False

prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix dce: <http://purl.org/dc/elements/1.1/>
prefix dct: <http://purl.org/dc/terms/>
prefix pav: <http://purl.org/pav/>
prefix prov: <http://www.w3.org/ns/prov#>

select ?np ?title ?date ?appends (count(distinct ?sub) as ?subindex_count) where {
# Commented out for performance reasons under Virtuoso:
#  graph npa:graph {
#    ?np npa:hasHeadGraph ?h .
#  }
  graph ?h {
    ?np np:hasAssertion ?a .
    ?np np:hasPublicationInfo ?i .
  }
  graph ?a {
# The line below is a workaround due to a strange bug in Virtuoso with 'optional' statements
# when the first statement within a graph:
    { optional { ?np npx:appendsIndex ?appends . } } union
    {
      optional { ?np npx:includesSubindex ?sub . }
      optional { ?np npx:appendsIndex ?appends . }
    }
  }
  graph ?i {
    ?np a npx:NanopubIndex .
    filter not exists { ?np a npx:IncompleteIndex . }
    optional { ?np dce:title ?title . }
    optional { ?np (dct:created|pav:createdOn|prov:generatedAtTime) ?date . }
  }
}
group by ?np ?title ?appends ?date
order by desc(?date)
