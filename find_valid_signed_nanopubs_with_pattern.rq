#+ summary: Find signed nanopublications that are not retracted or superseded for the given triple pattern
#+ method: GET
#+ pagination: 1000
#+ endpoint_in_url: False

prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>

select ?np ?graphpred ?subj ?pred ?obj ?date ?pubkey where {
  graph npa:graph {
    ?np npa:hasHeadGraph ?h .
    ?np npa:creationDay ?__day_iri .
    ?np npa:creationMonth ?__month_iri .
    ?np npa:creationYear ?__year_iri .
    ?np dct:created ?date .
    ?np npa:hasValidSignatureForPublicKey ?__pubkey_xsd_string .
    bind(?__pubkey_xsd_string as ?pubkey)
  }
  graph ?h {
    ?np ?__graphpred_iri ?g .
    bind(?__graphpred_iri as ?graphpred)
  }
  graph ?g {
    ?__subj_iri ?__pred_iri ?__obj_iri .
    bind(?__subj_iri as ?subj)
    bind(?__pred_iri as ?pred)
    bind(?__obj_iri as ?obj)
  }
  filter not exists {
    graph npa:graph {
      ?newversion npa:hasHeadGraph ?nh .
      ?newversion npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?nh {
      ?newversion np:hasPublicationInfo ?ni .
    }
    graph ?ni {
      ?newversion npx:supersedes ?np .
    }
  }
  filter not exists {
    graph npa:graph {
      ?retraction npa:hasHeadGraph ?rh .
      ?retraction npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?rh {
      ?retraction np:hasAssertion ?ra .
    }
    graph ?ra {
      ?somebody npx:retracts ?np .
    }
  }
}
